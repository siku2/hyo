@build-rust:
    echo "compiling rust to wasm"
    wasm-pack build \
        --dev --no-typescript \
        --target web --out-name index
    mkdir -p dist
    find pkg \( -iname "*.js" -o -iname "*.wasm" \) -type f -exec cp {} dist/ \;

@build-sass:
    echo "compiling sass"
    sass \
        --quiet --update --load-path=site/scss \
        site/scss/hyo.scss dist/hyo.css

@build-static:
    echo "moving static files"
    mkdir -p dist
    cp -r static/* dist

build: build-rust build-sass build-static

@serve: build
    simple-http-server \
        --index --nocache \
        --port 1234 --try-file "dist/index.html" \
        dist

watch:
    watchexec \
        --clear --no-shell -i dist \
        --restart just serve
